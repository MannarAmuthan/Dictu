/**
 * queue.du
 *
 * Testing the Queue module.
 *
 */
from UnitTest import UnitTest;

import Queue;

class TestQueue < UnitTest {
    const queueCap = 5;
    const testValue = "asdf1234";

    setUp() {
        this.queue = Queue.new(this.queueCap);
    }

    testQueueNew() {
        this.assertSuccess(this.queue);
        this.assertNotNil(this.queue.unwrap());
    }

    testQueuePush() {
        const res = this.queue.unwrap().push(testValue);
        this.assertSuccess(res);
    }

    testQueuePop() {
        this.queue.unwrap().push(this.testValue);
        const res = this.queue.unwrap().pop();
        this.assertSuccess(res);
        this.assertEquals(res.unwrap(), this.testValue);
    }

    testQueueCap() {
        this.assertEquals(this.queue.unwrap().cap(), this.queueCap);
    }

    testQueueLen() {
        this.queue.unwrap().push(this.testValue);
        this.assertEquals(this.queue.unwrap().len(), 1);
    }

    testQueueIsEmpty() {
        this.assertTruthy(this.queue.unwrap().isEmpty());
        this.queue.unwrap().push(testValue);
        this.assertFalsey(this.queue.unwrap().isEmpty());
    }

    testQueueIsFill() {
        this.assertFalsey(this.queue.unwrap().isFull());
        [1, 2, 3, 4, 5].map(def (x) => this.queue.unwrap().push(x));
        this.assertTruthy(this.queue.unwrap().isFull());
    }
}


TestQueue().run();
